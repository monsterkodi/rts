
class Train

    @: (@cfg) ->
        
        @path  = @cfg.track?.path
        @speed = 0.02
        @cars  = []
        
    addCar: (car) ->
        
        @cars.push car
        
    setColor: (color) ->
        
        for car in @cars
            car.setColor color
        
    update: (delta, timeSum) ->
        
        return if not @path

        advance = delta * @speed
        
        if @cfg.pingpong
            if @cfg.forward
                @cfg.pos += advance
                if @cfg.pos >= 1.0
                    @cfg.pos = 2.0 - @cfg.pos
                    @cfg.forward = false
            else
                @cfg.pos -= advance
                if @cfg.pos <= 0.0
                    @cfg.pos *= -1
                    @cfg.forward = true
        else
            @cfg.pos += advance
            if @cfg.pos >= 1.0
                @cfg.pos -= 1.0
            
        for car, index in @cars
            car.update delta, timeSum
            car.moveToPathPos @path, clamp 0 1 @cfg.pos-0.0135*index
            
module.exports = Train