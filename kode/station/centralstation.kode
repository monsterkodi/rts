###
 0000000  00000000  000   000  000000000  00000000    0000000   000    
000       000       0000  000     000     000   000  000   000  000    
000       0000000   000 0 000     000     0000000    000000000  000    
000       000       000  0000     000     000   000  000   000  000    
 0000000  00000000  000   000     000     000   000  000   000  0000000
###

CargoStation = require './cargostation'
ColorGrid = require '../lib/colorgrid'
Station = require './station'

GRID_SIZE = 10

function CentralStation extends CargoStation

    @: (cfg) ->
        
        cfg.name ?= "C#{Station.id+1}"
        super cfg
                
        label = world.addLabel text:'▴➜▪➜●' mono:true position:[0 -2.61 1.2], color:0xffffff, scale:1.4
        label.rotateX deg2rad 90
        @group.add label
        
        @arm.waitingForCar = true
        
        @grid = new ColorGrid gridSize:GRID_SIZE, size:4
        @grid.quads.rotateX deg2rad 90
        @grid.quads.position.z = 2.5
        @grid.quads.position.y = -2.61
        @group.add @grid.quads
        
        @gridShiftTime = 0
        @gridColumns = [[]]
        world.addAnimation @updateGrid
          
    hasCargo: -> false
    takesCargo: -> true        
    providesCargo: -> false
    
    carWaitingForUnload: (@waitingCar) ->
        
        if @arm.waitingForCar
            @arm.startUnloadingCar @waitingCar
            
    resourceIndex: (resource) -> 1 + Object.keys(Colors.mining).indexOf resource
            
    cargoStored: (resource) ->
        if @gridColumns[0].length == GRID_SIZE
            @gridShiftTime = 0
            @gridColumns.unshift []
        @gridColumns[0].push @resourceIndex resource
        @grid.setColumns @gridColumns
        
    updateGrid: (scaledDelta, timeSum) =>

        @gridShiftTime += scaledDelta
        if @gridShiftTime > 51*GRID_SIZE
            @gridShiftTime = 0
            @gridColumns.unshift []
            while @gridColumns.length > GRID_SIZE ➜ @gridColumns.pop()
            @grid.setColumns @gridColumns
            
        world.addAnimation @updateGrid
        
module.exports = CentralStation
