###
 0000000  000   000  00000000   000   000  00000000   0000000  000000000  00000000   000    
000       000   000  000   000  000   000  000       000          000     000   000  000    
000       000   000  0000000     000 000   0000000   000          000     0000000    000    
000       000   000  000   000     000     000       000          000     000   000  000    
 0000000   0000000   000   000      0      00000000   0000000     000     000   000  0000000
###

MIN_CTRL_DIST = 2

function CurveCtrl

    @active: new Set
    @deactivateAll: -> CurveCtrl.active.forEach (ctrl) -> ctrl.deactivate()

    @: (@track, @index) ->
        
        @curvePath  = @track.curve
        @curve      = @curvePath.curves[@index]
        @isLast     = @index == @curvePath.curves.length-1
        @isFirst    = @index == 0
        @isMid      = not @isFirst

        @curveStart = @curve.v0
        @ctrlStart  = @curve.v1
        @ctrlEnd    = @curve.v2
        @curveEnd   = @curve.v3
        
        @group = new Group
        @group.name = 'ctrl'
        
        if @isMid
            geom = new BoxGeometry 1 1 1
            mesh = new Mesh geom, Materials.ctrl.start
            @prevCtrlEnd = @curvePath.curves[@index-1].v2
            mesh.position.y = -@prevCtrlDist()
            mesh.onDrag = @onDrag 
            mesh.name = 'prevCtrlEnd'
            mesh.handler = @
            mesh.visible = false
            @meshPrevCtrlEnd = mesh
            @group.add @meshPrevCtrlEnd

            geom = new BoxGeometry 0.35 1 0.35
            mesh = new Mesh geom, Materials.ctrl.start
            mesh.scale.set 1 @prevCtrlDist(), 1
            mesh.position.y = -@prevCtrlDist()/2
            mesh.name = 'prevCtrlRod'
            mesh.visible = false
            @meshPrevCtrlRod = mesh
            @group.add @meshPrevCtrlRod
            
        if not @isFirst
            geom = Geom.cylinder heigt:0.71 radius:0.7 smnt:32
            mesh = new Mesh geom, Materials.ctrl.curve
            mesh.onDrag = @onDrag 
            mesh.onClick = @onCenterClick
            mesh.name = 'curveStart'
            mesh.handler = @
            mesh.setShadow()
            
            @meshCurveStart = mesh
            world.addPickable @meshCurveStart
            @group.add @meshCurveStart
        
        geom = new BoxGeometry 1 1 1
        mesh = new Mesh geom, Materials.ctrl.start
        if not @isFirst
            mesh.onDrag = @onDrag 
        else
            mesh.material = Materials.ctrl.transparent
            
        mesh.handler = @
        mesh.position.y = @ctrlStartDist()
        mesh.name = 'ctrlStart'
        mesh.visible = false
        @meshCtrlStart = mesh
        @group.add @meshCtrlStart

        geom = new BoxGeometry 0.35 1 0.35
        mesh = new Mesh geom, Materials.ctrl.start
        mesh.scale.set 1 @ctrlStartDist(), 1
        mesh.position.y = @ctrlStartDist()/2
        mesh.name = 'ctrlRod'
        mesh.visible = false
        @meshCtrlRod = mesh
        @group.add @meshCtrlRod
        
        @group.position.copy @curveStart
        @setDir @curve.getTangent 0, Vector.tmp
            
        world.addObject @group
        
    del: ->
        
        world.hideCompass @
        world.removeObject @group
        world.removePickable @meshCurveStart
        delete @group
        delete @meshCtrlStart
        delete @meshCurveStart
        delete @meshPrevCtrlEnd
        
    compassCenterDoubleClicked: =>
        
        post.emit 'convertCtrlToNode' @
        
    #  0000000    0000000  000000000  000  000   000  00000000  
    # 000   000  000          000     000  000   000  000       
    # 000000000  000          000     000   000 000   0000000   
    # 000   000  000          000     000     000     000       
    # 000   000   0000000     000     000      0      00000000  
    
    onCenterClick: => 
    
            if world.compass.object == @
                world.hideCompass @
            else
                world.showCompass @, @getPos(), @getDir() 
    
    onLeave: (hit) =>
        
        if hit.mesh.name == 'prevCtrlEnd'
            @meshPrevCtrlEnd?.material = Materials.ctrl.start
            @meshPrevCtrlRod?.material = Materials.ctrl.start
        if hit.mesh.name == 'ctrlStart'
            @meshCtrlStart?.material = Materials.ctrl.start
            @meshCtrlRod?.material   = Materials.ctrl.start
        if hit.mesh.name == 'curveStart'
            post.removeListener 'delete' @onDelete
            @meshCurveStart.material = Materials.ctrl.curve
        @
        
    onEnter: (hit) =>
        
        if hit.mesh.name == 'prevCtrlEnd'
            @meshPrevCtrlEnd?.material = Materials.ctrl.highlight
            @meshPrevCtrlRod?.material = Materials.ctrl.highlight
        if hit.mesh.name == 'ctrlStart'
            @meshCtrlStart?.material = Materials.ctrl.highlight
            @meshCtrlRod?.material   = Materials.ctrl.highlight
        if hit.mesh.name == 'curveStart'
            @meshCurveStart.material = Materials.ctrl.highlight
            post.on 'delete' @onDelete
            @activate()
        @
        
    onDelete: => log 'delete ctrl!'
    
    isActive: => CurveCtrl.active.has @
            
    activate: =>
        
        return if @isActive()
        
        CurveCtrl.active.add @
        
        @meshPrevCtrlEnd.visible = true
        @meshPrevCtrlRod.visible = true
        @meshCtrlStart.visible = true
        @meshCtrlRod.visible = true
        
        world.addPickable @meshPrevCtrlEnd
        world.addPickable @meshCtrlStart
        
    deactivate: =>

        world.hideCompass @
        
        return if not @isActive()
        
        CurveCtrl.active.delete @
        
        @meshPrevCtrlEnd?.visible = false
        @meshPrevCtrlRod?.visible = false
        @meshCtrlStart?.visible   = false
        @meshCtrlRod?.visible     = false
        
        @meshCurveStart?.material = Materials.ctrl.curve
        world.removePickable @meshPrevCtrlEnd
        world.removePickable @meshCtrlStart
                
    # 0000000    00000000    0000000    0000000   
    # 000   000  000   000  000   000  000        
    # 000   000  0000000    000000000  000  0000  
    # 000   000  000   000  000   000  000   000  
    # 0000000    000   000  000   000   0000000   
    
    prevCtrlDist:  -> @prevCtrlEnd.distanceTo @curveStart
    ctrlStartDist: -> @ctrlStart.distanceTo @curveStart
    
    onDrag: (hit, downHit) =>
        
        world.hideCompass @
        
        plane = new Plane
        plane.setFromNormalAndCoplanarPoint Vector.unitZ, @getPos()
        hit.ray.intersectPlane plane, Vector.tmp
        Vector.tmp.round()
                    
        if downHit.mesh.name == 'ctrlStart'
            
            ray = new Ray @curveStart, @getDir()
            newPos = vec()
            ray.closestPointToPoint Vector.tmp, newPos
            
            dist = newPos.distanceTo @curveStart
            return if dist < 1
            if dist < MIN_CTRL_DIST
                @ctrlStart.sub @curveStart
                @ctrlStart.normalize()
                @ctrlStart.multiplyScalar MIN_CTRL_DIST
                @ctrlStart.add @curveStart
                dist = @ctrlStartDist()
            else
                @ctrlStart.copy newPos
            
            @meshCtrlRod.position.y = dist/2
            @meshCtrlRod.scale.set 1 dist, 1
            @meshCtrlStart.position.y = dist
            
        if downHit.mesh.name == 'prevCtrlEnd'
            
            ray = new Ray @curveStart, @getDir().negate()
            newPos = vec()
            ray.closestPointToPoint Vector.tmp, newPos
            
            dist = newPos.distanceTo @curveStart
            return if dist < 1
            if dist < MIN_CTRL_DIST
                @prevCtrlEnd.sub @curveStart
                @prevCtrlEnd.normalize()
                @prevCtrlEnd.multiplyScalar MIN_CTRL_DIST
                @prevCtrlEnd.add @curveStart
                dist = @prevCtrlDist()
            else
                @prevCtrlEnd.copy newPos
            
            @meshPrevCtrlRod.position.y = -dist/2
            @meshPrevCtrlRod.scale.set 1 dist, 1
            @meshPrevCtrlEnd.position.y = -dist
            
        if downHit.mesh.name == 'curveStart'
            @setPos Vector.tmp
            
        @curve.updateArcLengths()
        @track.ctrlMoved()
                
    compassRotated: (dir) => @setDir dir
                
    setDir: (dir) ->
        
        quat = Quaternion.unitVectors Vector.unitY, dir
        @group.quaternion.copy quat
        
        @meshCtrlStart.getWorldPosition @ctrlStart
        @meshPrevCtrlEnd?.getWorldPosition @prevCtrlEnd
        
        @curve.updateArcLengths()
        @track.ctrlMoved()
        
    # 00     00   0000000   000   000  00000000  
    # 000   000  000   000  000   000  000       
    # 000000000  000   000   000 000   0000000   
    # 000 0 000  000   000     000     000       
    # 000   000   0000000       0      00000000  
    
    moveStartTo: (point) ->
        
        delta = Vector.tmp.copy point
               
        delta.sub @curveStart
        @curveStart.add delta
        @ctrlStart.add delta
        @meshCtrlStart.position.copy @ctrlStart
            
        @curve.updateArcLengths()

    moveEndTo: (point) ->
        
        delta = Vector.tmp.copy point
               
        delta.sub @curveEnd
        @curveEnd.add delta
        @ctrlEnd.add delta
        
        @curve.updateArcLengths()
        
    setPos: (point) ->
        
        delta = vec(point).minus @curveStart
        
        @curveStart.add delta
        @ctrlStart.add delta
        @prevCtrlEnd.add delta
        @group.position.copy @curveStart
        
        @curve.updateArcLengths()
        @track.ctrlMoved()

    getPos: -> @group.position
    getDir: -> vec(0 1 0).applyQuaternion @group.quaternion
        
    # 00000000    0000000   000000000   0000000   000000000  00000000  
    # 000   000  000   000     000     000   000     000     000       
    # 0000000    000   000     000     000000000     000     0000000   
    # 000   000  000   000     000     000   000     000     000       
    # 000   000   0000000      000     000   000     000     00000000  
    
    rotateStart: (dir) ->
        
        length = @ctrlStart.distanceTo @curveStart
        @ctrlStart.copy dir
        @ctrlStart.setLength length
        @ctrlStart.add @curveStart
        @curve.updateArcLengths()
        
    rotateEnd: (dir) ->
        
        length = @ctrlEnd.distanceTo @curveEnd
        @ctrlEnd.copy dir
        @ctrlEnd.setLength length
        @ctrlEnd.add @curveEnd
        @curve.updateArcLengths()
        
module.exports = CurveCtrl
