
Path = require '../track/path'
    
function Train
    
    @numTrains: 0

    @: (@cfg) ->
        
        Train.numTrains++
        @name     = @cfg.name ? "T" + Train.numTrains
        @path     = new Path @
        @speed    = @cfg.speed ? 1
        @topSpeed = @speed
        @cars     = []
        
    addCar: (car) ->
        
        @cars.push car
        
    setColor: (color) ->
        
        for car in @cars
            car.setColor color
            
    addPreview: ->
        
        @preview = @cars[0].mesh.clone()
        @preview.material = Materials.transparent
        for c in @preview.children
            c.material = Materials.transparent
        world.scene.add @preview
        
    block:   -> @speed = 0
    unblock: -> @speed = @topSpeed
    
    tailDelta:     -> @path?.normDelta @path.delta-4.2*(@cars.length-1)
    tailTrack:     -> @path?.trackAtDelta @tailDelta()
    prevTailTrack: -> @path?.tracks[@path.normIndex @path.indexAtDelta(@tailDelta())-1]
    tailPrevDistance: -> @path?.prevDistance @tailDelta()
                
    update: (delta, timeSum) ->
        
        return if not @path

        advance = delta * @speed
        
        advance = world.traffic.allowTrainAdvance @, advance
        
        @path.advance advance
                
        for car, index in @cars
            car.update delta, timeSum, @
            car.moveToPathDelta @path, -4.2*index
            
        if @preview
            #@path.moveMesh @preview, 8
            # log @path.delta, @path.currentIndex(), @path.nextNode().name
            @preview.position.copy @path.nextNode().position
            # @preview.needsUpdate = true
                        
module.exports = Train
