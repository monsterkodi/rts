###
0000000    000   0000000   000        0000000    000   000  000000000  000000000   0000000   000   000
000   000  000  000   000  000        000   000  000   000     000        000     000   000  0000  000
000   000  000  000000000  000        0000000    000   000     000        000     000   000  000 0 000
000   000  000  000   000  000        000   000  000   000     000        000     000   000  000  0000
0000000    000  000   000  0000000    0000000     0000000      000        000      0000000   000   000
###

{ clamp, deg2rad, drag } = require 'kxk'

CanvasButton = require './canvasbutton'

class DialButton extends CanvasButton

    @: (div, clss) ->
    
        super div, clss, vec(-4,4,6), vec(0,0,1).normal().mul 10
        
        @scene.background = Color.menu.background
        @name = 'DialButton'
        
        @drag = new drag
            target:  @canvas
            onStart: @onDrag
            onMove:  @onDrag
        
    setDial: (index) -> @update()

    initCamera: ->

        s = 5.5
        @camera = new THREE.OrthographicCamera -s, s, s, -s, 1, 10
        
    initScene: ->
        
        super()
        @initDots()
        
    initDots: ->
        
        geoms = []
        for i in [-6..6]
            geom = Geometry.sphere 0.5
            geom.rotateX deg2rad 90
            p = vec(0,4,0).rotate vec(0,0,1), i*22.5
            geom.translate p.x, p.y, p.z
            geoms.push geom
        
        merg = THREE.BufferGeometryUtils.mergeBufferGeometries geoms
        mesh = new THREE.Mesh merg, Materials.menu.inactive
        @scene.add mesh
        
        geom = Geometry.sphere 0.6
        geom.rotateX deg2rad 90
        @dot = new THREE.Mesh geom, Materials.menu.active
        @scene.add @dot
        
    onDrag: (drag, event) => 
        
        br = @canvas.getBoundingClientRect()
        
        ctr2Pos = vec(br.left+50, br.top+50).to drag.pos
        ctr2Pos.y = -ctr2Pos.y
        angle = Math.sign(ctr2Pos.dot(vec 1,0,0)) * ctr2Pos.angle(vec 0,1,0)
        sectn = clamp -6, 6, Math.round angle/22.5
        
        @setDial sectn
        
module.exports = DialButton
